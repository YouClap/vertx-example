plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC14'
}

ext {
    kotlinVersion = '1.3.20'
    vertxVersion = '3.7.0'
    rxKotlinVersion = '2.3.0'

    junitJupiterEngineVersion = '5.4.0'

    detektVersion = '1.0.0-RC14'
}

group = 'tech.youclap'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

application {
    mainClassName = 'io.vertx.core.Launcher'
}


def mainVerticleName = 'tech.youclap.example.verticle.MainVerticle'


dependencies {
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation "io.vertx:vertx-web:$vertxVersion"

    implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
    implementation "io.vertx:vertx-rx-java2:$vertxVersion"
    implementation "io.vertx:vertx-config:$vertxVersion"

    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"

    testImplementation "io.vertx:vertx-web-client:$vertxVersion"
    testImplementation "io.vertx:vertx-junit5:$vertxVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}


shadowJar {
    classifier = 'fat'
//    minimize()
    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

detekt {
    toolVersion = detektVersion
    input = files("src/main/kotlin")
}
